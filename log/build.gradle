plugins {
    id 'wutils.java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    maven {
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

configurations {
    shadedCompile
}

dependencies {
    compileOnly 'org.bukkit:bukkit:1.15.2-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.2'
    shadedCompile compileOnly('org.apache.commons:commons-lang3:3.12.0')
    shadedCompile compileOnly('net.kyori:adventure-text-minimessage:4.14.0')
    implementation project(':WUtils-config')
}

shadowJar {
    archiveClassifier.set('')
    minimize()
    configurations = [project.configurations.shadedCompile]
    relocate "org.apache.commons.lang3", "me.wyne.wutils.shadow.apache.commons.lang3"
    relocate "net.kyori", "me.wyne.wutils.shadow.kyori"
}

version = '2.0.8'

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'WUtils-log'
            version = version

            from components.java
        }
    }
}